type ppbm_fc = {
  magic_bytes: [byte],
  height: usize,
  width: usize,
  img: [byte]
}

format {
  MagicBytes := !"P1"!;;
  Whitespace := (# [" " | "\t" | "\r" | "\n"] #);;  // missing VT, FF (vertical tabulation, form feed)
  EndLineWhitespace := (# ["\r" | "\n"] #);;
  NonEndLineWhitespace := (# [Whitespace \ EndLineWhitespace] #);;
  DimensionValue := (#[DigitS]+#);;
  BinaryDigit := (# ["0" | "1"] #);;
  Comment := !"#"! (# [AsciiCharS \ EndLineWhitespace]*  EndLineWhitespace #);;
  Junk := (# Whitespace AsciiCharS* #)?;;

  LineCheck :=
    clone_view = {;; View.clone(View.get_current())}
    line = @[clone_view, (# [AsciiCharS \ EndLineWhitespace]* EndLineWhitespace #) ]
    [List.length(line) <=_u 70u];;

  TokenSeparatorLine := (# LineCheck NonEndLineWhitespace* (Comment | EndLineWhitespace) #);;
  PartialTokenSeparatorLine := (# LineCheck NonEndLineWhitespace #);;

  TokenSeparator := (# TokenSeparatorLine* (LineCheck Whitespace) TokenSeparatorLine* PartialTokenSeparatorLine* #);;
  RasterSeparator := (# (LineCheck Comment)* LineCheck Whitespace #);;

  ReadRasterLine rrl (width: isize) {img_raster_line: [byte]} :=
      ([width >_i 0i]
       img_val = BinaryDigit
       rrl_res = ReadRasterLine<width = width -_i 1i>
       {
         rrl.img_raster_line := img_val[0u]::rrl_res.img_raster_line
       })
    | ([width >=_i 0i] // this guard isn't necessary, but I think it improves clarity
       NonEndLineWhitespace
       rrl_res = ReadRasterLine<width = width>
       {
         rrl.img_raster_line := rrl_res.img_raster_line
       })
    | ([width = 0i]
       EndLineWhitespace
       {
         rrl.img_raster_line := []
       });;

  ReadRaster rr (width: isize, height: isize) {img_raster: [byte]} :=
    LineCheck
    img_raster_line = ReadRasterLine<width = width>
    img_raster_rest = ReadRaster<width = width, height = height -_i 1i>
    {
      rr.img_raster := img_raster_line::img_raster_rest
    };;

  PBM_FC fc {ppbm_fc} :=
    (| raster_width: usize := 0u, raster_height: usize := 0u |)
    magic_bytes = MagicBytes
    TokenSeparator
    height = DimensionValue
    [Usize.of_bytes(height) ~~ option::Some]
    TokenSeparator
    width = DimensionValue
    [Usize.of_bytes(width)  ~~ option::Some]
    {
      raster_width := Usize.of_bytes_unsafe(width);
      raster_height := Usize.of_bytes_unsafe(height)
    }
    RasterSeparator
    img = ReadRaster<width = Isize.of_bytes_unsafe(width), height = Isize.of_bytes_unsafe(height)>
    junk = Junk
    {
      fc.magic_bytes := magic_bytes;
      fc.height := raster_height;
      fc.width := raster_width;
      fc.img := img
    }
}
