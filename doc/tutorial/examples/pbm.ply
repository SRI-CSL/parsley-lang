// This Parsley file corresponds to the
// Plain PBM specification: http://netpbm.sourceforge.net/doc/pbm.html#plainpbm

type ppbm_fc = {
  magic_bytes: [byte],
  height: usize,
  width: usize,
  img: [byte]
}

format {
  MagicBytes := !"P1"!;;
  Whitespace := (# [" " | "\t" | "\r" | "\n"] #);;  // missing VT, FF
  EndLineWhitespace := (# ["\r" | "\n"] #);;
  NonEndLineWhitespace := (# [Whitespace \ EndLineWhitespace] #);;
  DimensionValue := (#[DigitS]+#);;
  BinaryDigit := (# ["0" | "1"] #);;
  Comment := !"#"! (# [AsciiCharS \ EndLineWhitespace]*  EndLineWhitespace #);;
  Junk := (# Whitespace AsciiCharS* #)?;;

  LineCheck {} :=
    clone_view = {;; View.clone(View.get_current())}
    line = @[clone_view, (# [AsciiCharS \ EndLineWhitespace]*
			    EndLineWhitespace #) ]
    [List.length(line) <=_u 70u];;

  TokenSeparatorLine := (# LineCheck NonEndLineWhitespace*
			   (Comment | EndLineWhitespace) #);;
  PartialTokenSeparatorLine := (# LineCheck NonEndLineWhitespace #);;

  TokenSeparator := (# TokenSeparatorLine* (LineCheck Whitespace)
		       TokenSeparatorLine* PartialTokenSeparatorLine* #);;
  RasterSeparator := (# (LineCheck Comment)* LineCheck Whitespace #);;

  ReadRasterLine rrl (pixels_left: isize) {pixels_read: [byte],
					   pixels_left_new: isize} :=
    [pixels_left >_i 0i]
    img_val = BinaryDigit
    rrl_res = ReadRasterLine<pixels_left = pixels_left -_i 1i>
    {
      rrl.pixels_read := img_val[0u]::rrl_res.pixels_read;
      rrl.pixels_left_new := rrl_res.pixels_left_new
    }
  ; [pixels_left >=_i 0i] // this guard is not strictly necessary
    NonEndLineWhitespace
    rrl_res = ReadRasterLine<pixels_left = pixels_left>
    {
      rrl.pixels_read := rrl_res.pixels_read;
      rrl.pixels_left_new := rrl_res.pixels_left_new
    }
  ; [pixels_left >=_i 0i] // this guard is not strictly necessary
    EndLineWhitespace
    {
      rrl.pixels_read := [];
      rrl.pixels_left_new := pixels_left
    };;

  ReadRaster rr (pixels_left: isize) {raster_image: [byte]} :=
    [pixels_left >_i 0i]
    LineCheck
    rrl = ReadRasterLine<pixels_left = pixels_left>
    rr_rest = ReadRaster<pixels_left = rrl.pixels_left_new>
    {
     rr.raster_image := List.concat(rrl.pixels_read, rr_rest.raster_image)
    }
  ; [pixels_left = 0i]
    {
      rr.raster_image := []
    };;

  PBM_FC fc {ppbm_fc} :=
    (| raster_width: usize := 0u, raster_height: usize := 0u,
       raster_size:  usize := 0u |)
    magic_bytes = MagicBytes
    TokenSeparator
    width = DimensionValue
    [Usize.of_bytes(width)  ~~ option::Some]
    TokenSeparator
    height = DimensionValue
    [Usize.of_bytes(height) ~~ option::Some]
    {
      raster_width := Usize.of_bytes_unsafe(width);
      raster_height := Usize.of_bytes_unsafe(height);
      raster_size := raster_width *_u raster_height
    }
    RasterSeparator
    img = ReadRaster<pixels_left = raster_size>
    Junk
    {
      fc.magic_bytes := magic_bytes;
      fc.height := raster_height;
      fc.width := raster_width;
      fc.img := img
    }
}
