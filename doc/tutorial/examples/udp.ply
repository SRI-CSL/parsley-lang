// This Parsley file is based on the UDP specification: https://tools.ietf.org/html/rfc768

// We make no assumption about whether we are using IPv4 or IPv6.
// However, we also don't perform some of the requisite checks
// using length and checksum as they would require knowing what
// version of IP we are using.

type udp_packet = {
  source_port: int,
  destination_port: int,
  length: int,
  checksum: [byte],
  data: [byte]
}

format {
  UDPPacket udpp (e: endian) {u: udp_packet} :=
    source_port = UInt16<endian=e>
    destination_port = UInt16<endian=e>
    length = UInt16<endian=e>
    checksum = (Byte^2) 
    data = (Byte^(length - 8)) // subtract 8 for the UDP header length
    [length >= 8] // length field must be at least 8, the UDP header length
    {
      udpp.u.source_port := source_port;
      udpp.u.destination_port := destination_port;
      udpp.u.length := length;
      udpp.u.checksum := checksum;
      udpp.u.data := data
    }
}

