= The expression sub-language
:sectanchors:

The functional language embedded in Parsley is a standard strict
expression-oriented language, essentially a simple version of the core
languages of Standard ML or OCaml.  It is simpler than those languages
since it does not support first-class functions and closures.  Its
main use in Parsley is to allow the user to define types and functions
that are used for computations performed within the grammar-oriented
fragments of the Parsley specification.

== Types

The type system has built-in atomic types, some standard polymorphic
type constructors, and support for polymorphic user-defined types.

=== Atomic types

The basic atomic types in this language are `byte`, `int`, `double`,
`string`, `bool`, `endian`, `bit` and `unit` (known as `void` in
C-like languages).  In addition, the language includes parsing buffers
as atomic values, which belong to a special atomic type called `view`.

=== Type constructors

The built-in type constructors are lists, tuples and options.
User-defined types typically use the variant and record type
constructors.

Lists and tuples have syntactic support: lists are denoted with `[`
and `]` enclosing the element type, while tuples are denoted within
`(` and `)` with `,` separating the tuple element types.

Bitvectors are also supported as special type constructors that take
constant positive numbers as arguments instead of other types.  A
bitvector of length `n` (e.g. 3) is specified as `bitvector<n>`
(e.g. `bitvector<3>`).

User-defined types are introduced with the `type` keyword, and must
begin with a lowercase letter.
....
 type list_of_bytes   = [ byte ]
 type pair_of_ints    = ( int, int )
 type list_of_pairs   = [ (int, int) ]
 type list_of_bitvecs = [ bitvector<5> ]
....

NOTE: The `->` arrow type constructor used for function types, common
in higher-order functional languages, is not available in Parsley for
user-defined type definitions.  This makes Parsley a first-order
language, without support for closures and partial function
applications.

*Type variables* in polymorphic types are denoted with identifiers
prefixed by a `'` (tick).  They are declared as arguments to the type
constructor within `(` and `)` and separated by `,`.  A polymorphic
type is instantiated with the type arguments supplied within `<` and
`>` and separated by `,`.  Bitvector lengths cannot be polymorphic.

....
 type list_of_generic_pairs ('a, 'b) = [ ('a, 'b) ]
 type list_of_int_pairs = list_of_generic_pairs<int,int>
....

*Variant types* are declared as follows, as a sequence of data
constructors separated by `|`, where the data constructors are
required to have an initial uppercase letter.

....
 type my_list =
 | Null
 | Cons of int * my_list

 type tree ('a) =
 | Node of tree<'a> * 'a * tree<'a>
 | Leaf of 'a
....

When used to construct values or patterns, the data constructors of a
variant type need to be prefixed with the type.  For example, some
values in the `my_list` type are `my_list::Null()` and
`my_list::Cons(2, my_list::Null())`.  Note in particular that nullary
data constructors such as `Null` in this example require `()` as an
argument to generate a value of `my_list`.

The `bool` built-in type is a variant type, with values of
`bool::True()` and `bool::False()`.  The `endian` type is similar,
with values of `endian::Big()` and `endian::Little()`.

The `bit` type is also a variant type, with values of `bit::Zero()`
and `bit::One()`.

*Record types* are declared using the following syntax:

....
 type point ('a, 'b) =
 { xcoord: 'a,
   ycoord: 'b }
....

For example, one value of the record type `point<int,int>` is
`{xcoord: 1, ycoord: 2}`.

Fields of record values are accessed through the usual `.` operator.
For example, if `p` is a variable of the above `point` type, its
fields are accessed as `p.xcoord` and `p.ycoord`.  Fields, unlike data
constructors, do not require a type prefix.  However, this means that
they need to be globally unique: two record types cannot use the same
field name.

*Bitfields* are specialized records that allow naming of contiguous
and non-overlapping ranges of bitvectors, starting from index `0`.  An
index range must be specified with the high-index preceding the
low-index and separated by a `:`.  A field that is 1-bit wide can use
the index position instead of a range as the range specifier.

Bitfields are declared using the following syntax:

....
 bitfield bf = {
   top: 8:7,
   mid: 6,
   bot: 5:0
 }
....

If `f` is a variable of the above `bf` type, its fields `f.top`,
`f.mid` and `f.bot` are of types `bitvector<2>`, `bitvector<1>` and
`bitvector<6>`.

NOTE: Currently, there cannot be an internal gap between the index
ranges that leaves indices unnamed by a field.

Types can also be *mutually recursive*.  Mutually recursive types are
declared in a single `type` declaration and separated by `and` within
that declaration:

....
 type dict ('a) =
 { d: (int, node<'a>) }

 and node ('a) =
 | Leaf of 'a
 | Dict of dict<'a>
....

=== Library types

The Parsley standard library provides polymorphic data structures such
as `set` (used as `set<element>`, where `element` is the element
type), and `map` (used as `map<k, v>`, where `k` and `v` are the types
of the key and value).  In addition, the library provides a `string`
type that differs from the `list<byte>` type in that its content
conforms to a valid character encoding.

NOTE: Full support for character encodings in strings is not yet
present.

== Constant definitions

Constants are declared with the `const` keyword, and with an explicit
type and value expression, with the following syntax:

....
 const max_packet_length : int = 1554
....

Constants cannot have polymorphic type.

== Function declarations

Functions are declared with the `fun` keyword, and with explicit
parameter types and a return type, with the following syntax:

....
 fun max (a: int, b: int) -> int = {
   (case a < b of
   | bool::True()  -> b
   | bool::False() -> a
   )
 }
....

Functions can be *polymorphic*, and type variables are indicated using a
similar syntax as type declarations, except that they are declared
within `<` and `>`.

....
 fun cons <'a> (l: my_list<'a>, e: 'a) -> my_list<'a> = {
   (case l of
   | my_list::Null()     -> my_list::Cons(e, my_list::Null())
   | my_list::Cons(_, _) -> my_list::Cons(e, l)
   )
 }
....

Functions can be *self-recursive*; such functions are declared using
the `recfun` keyword:

....
 recfun len <'a> (l: my_list<'a>) -> int = {
   (case l of
   | my_list::Null()     -> 0
   | my_list::Cons(_, t) -> 1 + len(t)
   )
 }
....

Functions can also be *mutually-recursive*; a set of such functions
are declared with an initial `recfun` keyword, and separated by `and`:

....
type op =
 | Single
 | Multiple of [op]

recfun num_ops (o: op) -> int = {
  (case o of
   | op::Single() -> 1
   | op::Multiple(s) -> 1 + num_seq_ops(s, 0)
   )
}

and num_seq_ops (os: [op], acc: int) -> int = {
  (case os of
   | [] -> acc
   | hd :: tl -> num_ops(hd) + num_seq_ops(tl, acc)
  )
}
....

Functions are applied using the syntax `f(arg1, arg2, ...)`, as in the
`len` example above.  Partial application to form closures is not
supported.

== Expressions

The primitive expressions are *literal values*, which can either be
integer literals like `21`, strings like `"this is a string"`, or the
unit value `()`.  Literal values can also be constructed from data
constructors of variant and record types, such as `my_list::Null()`
and `{xcoord:1, ycoord:2}` above.  Bitvector literals are specified
with a leading `0b`, e.g. `0b010` is a literal of type `bitvector<3>`.
The `[]` literal represents the empty list.

*Variables* are denoted by identifiers containing alphanumeric
characters, `_` and `@`.  Variable names must begin with a lowercase
letter.

Expressions can be combined with unary and binary *operators*.  Unless
specified, the operators below are binary and take two arguments.

.Operators
[%header, cols=2*]
|===
| Operator symbol
| Semantics

| -
| Integer negation (unary)

| +
| Integer addition

| -
| Integer subtraction

| *
| Integer multiplication

| %
| Integer modulus

| /
| Integer division

| &&
| Boolean and

| \|\|
| Boolean or

| \<=
| Integer less-than-equal

| >=
| Integer greater-than-equal

| =
| Polymorphic equal

| !=
| Polymorphic not-equal

| +_s
| String concatenation

| ::
| List cons

| @
| List concatenation
|===

Expressions can be formed by function application.  Partial function
application or currying is not supported; all arguments to a function
must be provided.

Variables `v` or functions `f` provided by a module `M` can be
accessed as `M.v` and `M.f`.  Module names always begin with an
uppercase letter.

*Let* expressions allow for locally scoped variables, and are
 useful for abbreviation:
....
  let i = _complicated_expression_ in
  _expression_body_using_i_
....

It is often useful to restrict the type of an expression, either for
improved code readability, or for better type-checking diagnostics.
This is expressed as `(e : t)` where the expression `e` is restricted
to have the type `t`.

*Case* expressions allow pattern matching over the various
constructors of a variant type, as can be seen in the `len` function,
repeated below.  Case expressions need to be enclosed within
parentheses, and its branches are separated by `|`.  Each branch has
two parts separated by an `->`: a pattern to the left, and an
expression on the right.

*Patterns* are formed from primitive patterns such as literal values,
variables, and the wildcard pattern `_`.  They are composed with data
constructors of variant types or the tuple constructor.

....
 recfun len <'a> (l: my_list<'a>) -> int = {
   (case l of
   | my_list::Null()     -> 0
   | my_list::Cons(_, t) -> 1 + len(t)
   )
 }
....

The above example used a user-defined list `my_list`.  There is
syntactic support for the `list` of the standard library:

....
 recfun len <'a> (l: ['a]) -> int = {
   (case l of
   | []     -> 0
   | _ :: t -> 1 + len(t)
   )
 }
....

Bitvectors can be indexed with ranges to extract contiguous
subsequences of bits as bitvectors using the indexing operator, which
encloses the range within `[[` and `]]`.  For example, given a
bitvector `v` of type `bitvector<3>`,
....
  (v[[2:1]], v[[0]])
....
extracts the top two bits and the lowest bit into a tuple of two
bitvectors of type `(bitvector<2>, bitvector<1>)`.

*Bitfield conversions* allow the conversion between bitfields and
 bitvectors of the appropriate length.  `bf->bits(r)` converts a
 bitfield `r` of bitfield type `bf` into a bitvector `r` of length
 `n`, where `n` is the number of bits covered by the bitfield `bf`.
 Similarly, `bf->record(v)` converts a bitvector `v` of length `n`
 into a bitfield record of type `bf`, provided `bf` covers `n` bits.

=== Views

Parsley provides first-class access to *parsing buffers*, which are
presented as opaque values of the type `view`.  These values are
created and operated on using functions in the `View` module.  Values
of type `view` can be stored in and retrieved from data structures, and
synthesized attributes.  A value of type `view` can be considered to
represent the current state of a parsing buffer, viz. a bytestream of
a specific bounded size, and a cursor location within those bounds.

The most important use of parsing buffers is in the grammar sub-language, which
provides view-mapping combinators that perform parsing actions within
the bounds of a particular view.  Since view computations are
performed within the expression sub-language, but used within the
grammar sub-language, they need to be communicated across this
'language barrier'.  This is done using the variables bound to action
blocks, where the last expression in the block can return a value to
the binding variable of the block, as described in the
<<grammar.adoc#rules-and-rule-elements,grammar documentation>>.

Although all the values of other primitive types in the expression
sub-language are stateless, values of type `view` are stateful.  When
used in a successfully matching parsing rule, the cursor location is
updated to point past the bytes consumed by the parsing rule.
Similarly, if the parsing rule fails after a partial match, the cursor
location is rewound to its original location at the start of that
parsing rule, before the next rule in any ordered choice is applied.

Although values of type `view` are opaque, they can be compared for
equality and inequality using the generic `=` and `!=` operators.

Different values of type `view` have independent state.  View values
created or cloned from originating parent views do not affect the
parent views when the child views are used in a parsing action.

=== Exceptions

The evaluation of expressions can result in failure; this results in
an exception being thrown within the Parsley execution engine, and an
error being returned to the application using the engine.

NOTE: The parsing state after this exception is currently unspecified.

== Modules

Parsley supports the use of definitions from one Parsley
specification, considered as a *module*, within another Parsley
specification.  The name of the Parsley module for a specification is
derived from the top-level file for that specification by capitalizing
the first character of the filename and omitting any suffix.  For
example, a specification with top-level file `gram.ply` corresponds to
a Parsley module called `Gram`.

Functions `f`, constants `c`, and types `t` from a Parsley module `M`
are specified with the syntax `M.f`, `M.c` and `M.t` respectively.  A
data constructor `D(...)` for a type `t` in module `M` is specified
with the syntax `M.t::D(...)` (as compared to its within-module syntax
of `t::D(...)`).  If a record field `f` is defined in module `M`, that
field can be extracted from an expression `e` with the syntax
`e.(M.f)` (as compared to the within-module syntax of `e.f`).

Parsley does not support nested modules.

=== Conflicts with the standard library

Parsley forbids specifications whose module names conflict with the
modules in the standard library.  This means that it does not allow
_top-level_ specifications to be written in files with names like
`list.ply`, `view.ply`, etc.  It does however support top-level
specifications that `include` files with these names.  That is, a
specification in a top-level file called `gram.ply` can include a file
called `list.ply`.

Identifiers with a module qualifier, e.g. `M.i` using the syntax
described above, are called *explicitly qualified* identifiers.
Identifiers `i` without such qualification are *implicitly qualified*.
When processing the identifiers in the specification in a top-level
file `gram.ply`, Parsley resolves the module membership of an
implicitly qualified identifier using the following algorithm:

. If it is within the lexical scope of a local declaration, it is
qualified to belong to the parent `Gram` module.  Top-level type and
function definitions are not considered local declarations; they are
global declarations in that module.  Examples of local declarations
are function parameters, the local name for a non-terminal used within
its definition, and local bindings for rule elements.

. Otherwise, if there is a standard library identifier with the same
name, it is qualified to belong to the standard library module.

. Otherwise, it is qualified to belong to the parent `Gram` module.

It is possible to define entities with names that conflict with names
in the standard library; one only needs to refer to them using an
explicitly qualified identifier.  For example, if the specification
`gram.ply` defines a type called `int`, it can refer to that type
where needed as `Gram.int`; an implicitly qualified type identifier
named `int` in `gram.ply` will continue to refer to the type `int`
from the Parsley standard library.  The handling of name conflicts in
other definitions, such as constants, functions, and non-terminals, is
similar.
