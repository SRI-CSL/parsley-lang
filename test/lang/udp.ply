// This Parsley file is based on the UDP specification: https://tools.ietf.org/html/rfc768

// We make no assumption about whether we are using IPv4 or IPv6.
// However, we also don't perform some of the requisite checks
// using length and checksum as they would require knowing what
// version of IP we are using.

type udp_packet = {
  source_port: u16,
  destination_port: u16,
  length: usize,
  checksum: [byte],
  data: [byte]
}

format {
  UDPPacket udpp {u: udp_packet} :=
    source_port = UInt16<endian=endian::Big()>
    destination_port = UInt16<endian=endian::Big()>
    length = UInt16<endian=endian::Big()>
    checksum = (Byte^2u)
    data = (Byte^(Usize.of_u16(length) -_u 8u)) // subtract 8 for the UDP header length
    [length >=_u16 8u16] // length field must be at least 8, the UDP header length
    {
      udpp.u.source_port := source_port;
      udpp.u.destination_port := destination_port;
      udpp.u.length := Usize.of_u16(length);
      udpp.u.checksum := checksum;
      udpp.u.data := data
    }
}

