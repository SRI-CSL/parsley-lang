// Parsley: Foundations of Safe Parsing
// 
// NITF: Image Sub-header Parsley Definition
// 
// SRI International, 2022

include character_sets

fun is_valid_isclsy_1(isclas: [byte], isclsy: [byte]) -> bool = {
    (case isclas of
        | "U" -> (case isclsy of
                    | "  " -> bool::True()
                    | _ -> bool::False()
                )
        | _ -> (case isclsy of
                    | "  " -> bool::False()
                    | _ -> bool::True()
                )
    )
}


type image_subheader = {
    im: [byte],         // file part type, size: 2, type: R
    iid1: [byte],       // image identifier 1, size: 10, type: R
    idatim: [byte],     // image date and time, size: 14, type: R
    tgtid: [byte],      // target identifier, size: 17, type: R
    iid2: [byte],       // image identifier 2, size: 80, type: <R>
    isclas: [byte],     // image security classification, size: 1, type: R
    isclsy: [byte],     // image security classification system, size: 2, type <R>
    iscode: [byte],     // image codewords, size: 11, type <R>
    isctlh: [byte],     // image control and handling, size: 2, type <R>
    isrel: [byte],      // image releasing instructions, size: 20, type <R>
    isdctp: [byte],     // image declassification type, size: 2, type <R>
    isdcdt: [byte],     // image declassification date, size: 8, type <R>
    isdcxm: [byte],     // image declassification exemption, size: 4, type <R>
    isdg: [byte],       // image downgrade, size: 1, type <R>
    isdgdt: [byte],     // image downgrade date, size: 8, type <R>
    iscltx: [byte],     // image classification text, size: 43, type <R>
    iscatp: [byte],     // image classification authority type, size: 1, type <R>
    iscaut: [byte],     // image classification authority, size: 40, type <R>
    iscrsn: [byte],     // image classification reason, size: 1, type <R>
    issrdt: [byte],     // image security source date, size: 8, type <R>
    isctln: [byte],     // image security control number, size: 15, type <R>
    encryp: [byte],     // encryption, size: 1, type R
    isorce: [byte],     // image source, size: 42, type <R>
    nrows: [byte],      // number of significant rows in image, size: 8, type R
    ncols: [byte]       // number of significant columns in image, size: 8, type R
}
// todo: ENCRYp pg. 32 onwards

format {
    IM := !"IM"!;;
    IID1 := (# BCS_A ^ 10u #);;
    IDATIM := (# CCYYMMDDhhmmss #);;

    BE := (# BCS_A ^ 10u #);;
    OSUFFIX := (# BCS_A ^ 5u #);;
    CountryCode := (# BCS_A ^ 2u #);;
    BOC := (# BE OSUFFIX CountryCode #);;
    TGTID := (# BOC | ["\x20"] ^ 17u #);;

    IID2 := (# ECS_A ^ 80u | ["\x20"] ^ 80u #);;
    ISCLAS := (# ["T" | "S" | "C" | "R" | "U" ] #);;
    ISCLSY := (# ECS_A ^ 2u #);;
    ISCODE := (# ECS_A ^ 11u #);;
    ISCTLH := (# ECS_A ^ 2u #);;
    ISREL := (# ECS_A ^ 20u #);;
    ISDCTP := (# ["DD" | "DE" | "GD" | "GE" | "O" | "X" | "\x20\x20"] #);;
    ISDCDT := (# CCYYMMDD | ["\x20"] ^ 8u #);;
    ISDCXM := (# ["X1" .. "X8"] | ["25X1" .. "25X9"] | ["DNIO"] | ["\x20"] ^ 4u #);;
    ISDG := (# ["S" | "C" | "R" | "\x20"] #);;
    ISDGDT := (# CCYYMMDD | ["\x20"] ^ 8u #);;
    ISCLTX := (# ECS_A ^ 43u #);;
    ISCATP := (# ["O" | "D" | "M" | "\x20"] #);;
    ISCAUT := (# ECS_A ^ 40u #);;
    ISCRSN := (# ["A" .. "H"] | ["\x20"] #);;
    ISSRDT := (# CCYYMMDD | ["\x20"] ^ 8u #);;
    ISCTLN := (# ECS_A ^ 20u #);;
    ENCRYP := (# BCS_N_INT_POS ^ 1u #);;
    ISORCE := (# ECS_A ^ 42u #);;
    NROWS := (# ["00000001" .. "99999999"] #);;
    NCOLS := (# ["00000001" .. "99999999"] #);;

    ImageSubheader img_sub {image_subheader} := 
        file_part_type = IM
        { img_sub.im := file_part_type }

        image_identifier_1 = IID1
        { img_sub.iid1 := image_identifier_1 }

        image_date_and_time = IDATIM
        { img_sub.idatim := image_date_and_time }

        target_identifier = TGTID
        { img_sub.tgtid := target_identifier }

        image_identifier_2 = IID2
        { img_sub.iid2 := image_identifier_2 }

        image_security_classification = ISCLAS
        { img_sub.isclas := image_security_classification }

        // Fields from ISCODE until ISCTLN are placed before ISCLSY
        // per interpretation of NOTE at the top of Table 3, pg. 30
        image_codewords = ISCODE
        { img_sub.iscode := image_codewords }

        image_control_and_handling = ISCTLH
        { img_sub.isctlh := image_control_and_handling }

        image_releasing_instructions = ISREL
        { img_sub.isrel := image_releasing_instructions }

        image_declassification_type = ISDCTP
        { img_sub.isdctp := image_declassification_type }

        image_declassification_date = ISDCDT
        // [image_declassification_type = "DD"] todo: revisit this field
        { img_sub.isdcdt := image_declassification_date }

        image_declassification_exemption = ISDCXM
        { img_sub.isdcxm := image_declassification_exemption }

        image_downgrade = ISDG
        { img_sub.isdg := image_downgrade }

        image_downgrade_date = ISDGDT
        { img_sub.isdgdt := image_downgrade_date }

        image_classification_text = ISCLTX
        { img_sub.iscltx := image_classification_text }

        image_classification_authority_type = ISCATP
        { img_sub.iscatp := image_classification_authority_type }

        image_classification_authority = ISCAUT
        { img_sub.iscaut := image_classification_authority }

        image_classification_reason = ISCRSN
        { img_sub.iscrsn := image_classification_reason }

        image_security_source_date = ISSRDT
        { img_sub.issrdt := image_security_source_date }

        image_security_control_number = ISCTLN
        { img_sub.isctln := image_security_control_number }

        image_security_classification_system = ISCLSY
        [is_valid_isclsy_1(image_security_classification_system, image_security_classification)]
        { img_sub.isclsy := image_security_classification_system }

        encryption = ENCRYP
        { img_sub.encryp := encryption }

        image_source = ISORCE
        { img_sub.isorce := image_source }

        number_of_significant_rows_in_image = NROWS
        { img_sub.nrows := number_of_significant_rows_in_image }

        number_of_significant_cols_in_image = NCOLS
        { img_sub.ncols := number_of_significant_cols_in_image }
        
}